SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  ?cofog
WHERE {
  BIND(wd:Q20 AS ?country)

  {
    VALUES ?type {
      wd:Q755707 # Municipality of Norway (356)
    }

    ?org wdt:P31 ?type ;
         wdt:P17 ?country .
  } UNION {
    VALUES ?type {
      wd:Q3917681 # embassy (82)
    }
    ?org wdt:P31 ?type ;
         wdt:P137 ?country .
  } UNION {
    VALUES ?type {
      wd:Q192350 #ministry (16)
      }
      ?org wdt:P31/wdt:P279* ?type ;
           wdt:P17 ?country .
    }

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P3999 [] }

  OPTIONAL {
    ?org wdt:P9798/wdt:P279* ?cofog .
    MINUS { ?cofog wdt:P279 [] }
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,nb,no". }
}
ORDER BY ?type ?topic
